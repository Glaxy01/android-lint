apply plugin: 'com.android.application'
apply plugin: 'checkstyle'


android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.paincker.lint.demo"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode project.hasProperty('VERSION_CODE') ? Integer.parseInt(VERSION_CODE) : 1
        versionName project.hasProperty('VERSION_NAME') ? VERSION_NAME : ' 1.0.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

// 配置assemble任务依赖lint，编译时就会执行Lint检查
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def lintTask = tasks["lint${variant.name.capitalize()}"]
        output.assemble.dependsOn lintTask
    }
}

dependencies {
    // 依赖LintAAR，引入自定义Lint规则
    implementation project(path: ":lintaar")
}
